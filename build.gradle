import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
	id 'org.springframework.boot' version '2.6.7'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'com.cucumbertest'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
	testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.5.1'
	implementation group: 'io.cucumber', name: 'cucumber-java', version: '7.2.3'
	testImplementation group: 'io.cucumber', name: 'cucumber-junit', version: '7.2.3'
	implementation group: 'io.cucumber', name: 'cucumber-spring', version: '7.2.3'


	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.2'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'
	testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.8.2'

	implementation group: 'tech.grasshopper', name: 'extentreports-cucumber7-adapter', version: '1.3.0'
	implementation group: 'io.rest-assured', name: 'xml-path', version: '4.5.1'
	implementation group: 'io.rest-assured', name: 'json-path', version: '4.5.1'
}

// cucumber

configurations {
	cucumberRuntime {
		extendsFrom testImplementation
	}
}

task cucumberCli() {
	dependsOn assemble, testClasses
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
					'--plugin', 'pretty',
					'--plugin', 'com.aventstack.extentreports.cucumber.adapter.ExtentCucumberAdapter:',
					'--glue', 'com/digitalautomationsolution/dascucumber/stepDefinitions',
					'src/test/resources/featureFiles'
			]
		}
	}
}

test {
	useJUnitPlatform()

	testLogging {
		events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED
	}

	systemProperties(project.gradle.startParameter.systemPropertiesArgs)
}
